# STRIP_YAML_LINE()
# This function takes a raw yaml line as input and returns the stripped values
#     and the indentaton level
FUNCTION(STRIP_YAML_LINE LINE_STRING STRIPPED_LINE_STRING INDENTATION_LEVEL)
  # Strip comments form string
  STRING(REGEX REPLACE "\#.*$" "" LINE_STRING ${LINE_STRING})
  IF("${LINE_STRING}" STREQUAL "")
    # MESSAGE(STATUS "Empty line found")
    SET(${INDENTATION_LEVEL} "-1" PARENT_SCOPE)
    SET(${STRIPPED_LINE_STRING} "" PARENT_SCOPE)
    RETURN()
  ENDIF()

  # Strip leading/trailing spaces and comments
  STRING(REGEX REPLACE "^[ ]+" "" LINE_STRING_NO_INDENT ${LINE_STRING})
  IF("${LINE_STRING_NO_INDENT}" STREQUAL "")
    # MESSAGE(STATUS "Empty line found")
    SET(${INDENTATION_LEVEL} "-1" PARENT_SCOPE)
    SET(${STRIPPED_LINE_STRING} "" PARENT_SCOPE)
    RETURN()
  ENDIF()

  STRING(LENGTH ${LINE_STRING_NO_INDENT} LINE_LENGTH_STRIPPED)
  STRING(LENGTH ${LINE_STRING} LINE_LENGTH)
  MATH(EXPR INDENTATION "(${LINE_LENGTH} - ${LINE_LENGTH_STRIPPED}) / 2")

  STRING(REGEX REPLACE "[ \t]+$" "" LINE_STRING_NO_INDENT ${LINE_STRING_NO_INDENT})
  IF("${LINE_STRING_NO_INDENT}" STREQUAL "")
    # MESSAGE(STATUS "Empty line found")
    SET(${INDENTATION_LEVEL} "-1" PARENT_SCOPE)
    SET(${STRIPPED_LINE_STRING} "" PARENT_SCOPE)
    RETURN()
  ENDIF()

  SET(${STRIPPED_LINE_STRING} "${LINE_STRING_NO_INDENT}" PARENT_SCOPE)
  SET(${INDENTATION_LEVEL} "${INDENTATION}" PARENT_SCOPE)
ENDFUNCTION(STRIP_YAML_LINE)


# PARSE_YAML()
# Function to parse a yaml file

FUNCTION(PARSE_YAML YAML_PATH NODE_NAMES NODE_VALS)
  MESSAGE(STATUS "YAML_PATH: " ${YAML_PATH})
  FILE(STRINGS ${YAML_PATH} YAML_CONTENT)
  MESSAGE(STATUS "YAML_CONTENT: " ${YAML_CONTENT})

  SET(NODE_NAME_LIST "")
  SET(NODE_VALUE_LIST "")
  SET(NODE_TREE_LIST "")
  SET(LAST_NODE_IS_LISTING FALSE)
  SET(LAST_INDENTATION_LEVEL "0")
  SET(LAST_NODE_IS_BRANCH_NODE FALSE)

  FOREACH(YAML_NODE ${YAML_CONTENT})
    # MESSAGE(STATUS "YAML_NODE: " ${YAML_NODE})

    STRIP_YAML_LINE(${YAML_NODE} YAML_NODE INDENTATION_LEVEL)
    IF("${YAML_NODE}" STREQUAL "")
      # MESSAGE(STATUS "Empty line found")
      CONTINUE()
    ENDIF()
    IF("${INDENTATION_LEVEL}" STREQUAL "-1")
      MESSAGE(FATAL_ERROR "Indentation level invalid for line '${YAML_NODE}'")
      CONTINUE()
    ENDIF()

    # Find variable name
    STRING(REGEX MATCH "^[^:]+" NODE_NAME ${YAML_NODE})

    # Check if entry is part of a node list
    IF(${NODE_NAME} MATCHES "^-(.*)")
      # We are dealing with a list entry in the last node's NODE_NAME
      IF(${LAST_NODE_IS_LISTING})
        STRING(REPLACE "- " "" NODE_VALUE ${YAML_NODE})
        SET(NODE_NAME ${LISTING_NODE_NAME})
        SET(LAST_NODE_IS_BRANCH_NODE FALSE)
      ELSE()
        MESSAGE(FATAL_ERROR "Unexpected symbol '-' found. There is no list at '" ${NODE_NAME} "'")
      ENDIF()
    ELSE()
      # We are dealing with a new node entry
      IF(${YAML_NODE} MATCHES "(.*):$")
        # We have detected a node that has no entries, i.e. it is the beginning of a list
        SET(LAST_NODE_IS_LISTING TRUE)
        SET(LAST_NODE_IS_BRANCH_NODE TRUE)
        SET(NODE_VALUE "")
        # Store line if it was a node
        SET(LISTING_NODE_NAME "${NODE_NAME}")
        LIST(APPEND NODE_TREE_LIST "${NODE_NAME}")
      ELSE()
        # we are dealing with a one-line node
        STRING(REPLACE "${NODE_NAME}: " "" NODE_VALUE ${YAML_NODE})
        SET(LAST_NODE_IS_LISTING FALSE)
        SET(LAST_NODE_IS_BRANCH_NODE FALSE)
      ENDIF()
    ENDIF()

    # Set the exported list of variables
    IF(${LAST_NODE_IS_LISTING})
      # MESSAGE(STATUS ${LISTING_NODE_NAME} ": ${${LISTING_NODE_NAME}} ${NODE_VALUE}")
      SET(${LISTING_NODE_NAME} "${${LISTING_NODE_NAME}} ${NODE_VALUE}")

      LIST(FIND NODE_NAME_LIST ${LISTING_NODE_NAME} INDEX)
      # check if NODE_VALUE already exists
      IF(${INDEX} GREATER -1)
        LIST(REMOVE_AT NODE_VALUE_LIST ${INDEX})
        LIST(APPEND NODE_VALUE_LIST "${${LISTING_NODE_NAME}}")
      ELSEIF("${NODE_VALUE}" STREQUAL "")
        # MESSAGE("Skip as node is opening a list")
      ELSE()
        # MESSAGE("New list entry")
        LIST(APPEND NODE_NAME_LIST ${LISTING_NODE_NAME})
        LIST(APPEND NODE_VALUE_LIST ${NODE_VALUE})
      ENDIF()
    ELSE()
      # MESSAGE(STATUS ${NODE_NAME} ": ${NODE_VALUE}")
      SET(${NODE_NAME} "${NODE_VALUE}")
      LIST(APPEND NODE_NAME_LIST ${NODE_NAME})
      LIST(APPEND NODE_VALUE_LIST ${NODE_VALUE})
    ENDIF()
    SET(LAST_INDENTATION_LEVEL ${INDENTATION_LEVEL})
  ENDFOREACH()

  SET(${NODE_NAMES} "${NODE_NAME_LIST}" PARENT_SCOPE)
  SET(${NODE_VALS} "${NODE_VALUE_LIST}" PARENT_SCOPE)
  # # Uncomment to display all CMAKE variables:
  # GET_CMAKE_PROPERTY(VARIABLE_NAMES VARIABLES)
  # FOREACH (VARIABLE_NAME ${VARIABLE_NAMES})
  #    MESSAGE(STATUS "${VARIABLE_NAME}=${${VARIABLE_NAME}}")
  # ENDFOREACH()
ENDFUNCTION(PARSE_YAML)

FUNCTION(FIND_LIST_VALUE VAR_NAME NAME_LIST VAL_LIST VAL_OUTPUT)
  LIST(FIND NAME_LIST ${VAR_NAME} INDEX)
  IF(${INDEX} EQUAL -1)
    # MESSAGE(STATUS "var ${VAR_NAME} undefined..")
    RETURN()
  ENDIF()

  LIST(GET VAL_LIST ${INDEX} TMP)
  SET(${VAL_OUTPUT} "${TMP}" PARENT_SCOPE)
ENDFUNCTION(FIND_LIST_VALUE)
